; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#include <branding.include>

#include <source.include>

; Block below is constant.
#define Version "2.0"
#define RecorderExeName "Recorder.exe"
#define EditorExeName "Editor.exe"
#define MyAppUrlName "Homepage.url"

[Setup]
AppName={#MyAppName}
AppVerName={#MyAppName} {#Version}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputBaseFilename={#OutputBaseName}
OutputDir={#OutputFolder}
PrivilegesRequired=admin
Compression=lzma
SolidCompression=yes
WizardImageFile=compiler:WizModernImage-IS.bmp
#ifdef SetupLogo
WizardSmallImageFile={#SetupLogo}
#endif

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"

[Files]
Source: "{#Bin}\Webinaria.exe"; DestDir: "{app}"; DestName: "{#RecorderExeName}"; Flags: ignoreversion
Source: "{#Bin}\WebinariaEditor.exe"; DestDir: "{app}"; DestName: "{#EditorExeName}"; Flags: ignoreversion
Source: "{#UIFolder}*"; DestDir: "{app}\UI"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#Bin}\JPEG.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Bin}\SharedUI.dll"; DestDir: "{app}"; Flags: ignoreversion; Attribs: hidden system
Source: "{#Bin}\RmBkFilter.dll"; DestDir: "{app}"; Flags: ignoreversion regserver
Source: "{#Bin}\ffmpeg\*"; DestDir: "{app}\ffmpeg"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#Bin}\UScreenCapture.ax"; DestDir: "{app}"; Flags: onlyifdoesntexist regserver uninsrestartdelete
Source: "{#Bin}\htmlayout.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#Icon}"; DestDir: "{app}"; DestName: "app.ico"; Flags: ignoreversion
Source: "{#TrayIcon}"; DestDir: "{app}"; DestName: "tray.ico"; Flags: ignoreversion
#ifdef VideoSmallLogo
Source: "{#VideoSmallLogo}"; DestDir: "{app}\UI\Recorder"; DestName: "videologosmall.bmp"; Flags: ignoreversion
#endif
#ifdef VideoLargeLogo
Source: "{#VideoLargeLogo}"; DestDir: "{app}\UI\Recorder"; DestName: "videologolarge.bmp"; Flags: ignoreversion
#endif
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Dirs]
Name: "{userdocs}\{#DataFolder}"; Attribs: system; Flags: uninsneveruninstall
Name: "{app}\UI"; Attribs: hidden system; Flags: uninsalwaysuninstall

[Registry]
Root: HKLM; Subkey: "Software\Webinaria"; Flags: uninsdeletekey
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: ""; ValueData: {#MyAppName}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "HomepageUrl"; ValueData: {#MyAppURL}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "HelpUrl"; ValueData: {#HelpURL}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "DownloadUpdatesUrl"; ValueData: {#DownloadUpdatesURL}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "UpdateUrl"; ValueData: {#UpdateURL}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "PublishUrl"; ValueData: {#PublishURL}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "DataFolder"; ValueData: {#DataFolder}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "RecorderWindowTitle"; ValueData: {#RecorderWindowTitle}
Root: HKLM; Subkey: "Software\Webinaria"; ValueType: string; ValueName: "EditorWindowTitle"; ValueData: {#EditorWindowTitle}

[INI]
Filename: "{app}\{#MyAppUrlName}"; Section: "InternetShortcut"; Key: "URL"; String: "{#MyAppURL}"
Filename: "{userdocs}\{#DataFolder}\desktop.ini"; Section: ".ShellClassInfo"; Key: "IconFile"; String: "{app}\app.ico"

[Icons]
Name: "{group}\Make Recording"; Filename: "{app}\{#RecorderExeName}"; IconFileName: "{app}\app.ico"
Name: "{group}\Edit Recording"; Filename: "{app}\{#EditorExeName}"; IconFileName: "{app}\app.ico"
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{app}\{#MyAppUrlName}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#RecorderExeName}"; IconFileName: "{app}\app.ico"; Tasks: desktopicon

[Run]
Filename: "{app}\{#RecorderExeName}"; Description: "{cm:LaunchProgram,{#MyAppName}}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: "{app}\{#MyAppUrlName}"

